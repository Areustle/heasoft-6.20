      SUBROUTINE TTINIT()
C---
C Open a channel to the terminal that can be used for single character
C I/O.
C---
      INCLUDE '($iodef)'
      INTEGER   SYS$ASSIGN
C
      INTEGER   IOSTAT
C
      INTEGER*2 I2CHAN
      INTEGER   IRDTT, IWRTT
      COMMON /TTIO/ IRDTT, IWRTT, I2CHAN
C
      IRDTT = IO$_READVBLK.OR.IO$M_NOECHO.OR.IO$M_NOFILTR.OR.
     1 IO$M_ESCAPE
      IWRTT = IO$_WRITEVBLK
C
      IOSTAT=SYS$ASSIGN('SYS$COMMAND',I2CHAN,,)
      IF(IOSTAT.NE.1) STOP 'Unable to assign terminal channel'
C
      RETURN
      END
C*********
      SUBROUTINE TTRSET()
C---
C Close IO channel to terminal.  Restore terminal characteristics.
C---
      INTEGER*2 I2CHAN
      INTEGER   IRDTT, IWRTT
      COMMON /TTIO/ IRDTT, IWRTT, I2CHAN
C---
      CALL SYS$DASSGN(I2CHAN)
      RETURN
      END
C*********
      SUBROUTINE RDCHR (CHR)
      CHARACTER CHR*1
C---
C Read a single character from the assigned channel.
C---
      INTEGER IOSTAT, SYS$QIOW
C
      INTEGER*2 I2CHAN
      INTEGER   IRDTT, IWRTT
      COMMON /TTIO/ IRDTT, IWRTT, I2CHAN
C
      IOSTAT = SYS$QIOW (,%VAL(I2CHAN), %VAL(IRDTT),,,,%REF(CHR),
     1 %VAL(1),,,,,)
      IF (.NOT. IOSTAT) CALL LIB$SIGNAL (%VAL(IOSTAT))
C
      RETURN
      END
C
C*********
      SUBROUTINE PUTSTR (CBUF, LBUF)
      CHARACTER CBUF*(*)
      INTEGER   LBUF
C---
C Write CBUF(:LBUF) characters to the terminal.  If LBUF=0 then
C CBUF(:LENAC(LBUF)) characters are written.  It is very important
C that no other characters such as CR, LF, etc. are written.
C---
      INTEGER   LENACT, SYS$QIOW
      INTEGER   IOSTAT, ITMP
C
      INTEGER*2 I2CHAN
      INTEGER   IRDTT, IWRTT
      COMMON /TTIO/ IRDTT, IWRTT, I2CHAN
C---
      ITMP=LBUF
      IF(ITMP.EQ.0) ITMP=LENACT(CBUF)
      IOSTAT = SYS$QIOW (,%VAL(I2CHAN), %VAL(IWRTT),,,,
     1                %REF(CBUF),%VAL(ITMP),,,,,)
      IF (.NOT. IOSTAT) CALL LIB$SIGNAL (%VAL (IOSTAT))
C
      RETURN
      END
C*********
      SUBROUTINE FORTYP(IFTYPE)
      INTEGER   IFTYPE
C---
C IFTYPE should return
C  0  if single character IO (RDCHR, PUTSTR) has NOT been implemented.
C -1  if Fortran I/O preceeds write statements with a CR//LF.
C +1  if CR//LF follow Fortran write operations.
C---
      IFTYPE=-1
      RETURN
      END
C*********
      SUBROUTINE FLUSH(LUN)
      INTEGER   LUN
C---
C Write all information in the buffer to the terminal device.
C---
      RETURN
      END
