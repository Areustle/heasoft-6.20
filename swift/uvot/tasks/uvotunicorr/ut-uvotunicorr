#! perl
# $Source: /headas/headas/swift/uvot/tasks/uvotunicorr/ut-uvotunicorr,v $
# $Revision: 1.2 $
# $Date: 2012/01/31 15:57:00 $
#
# $Log: ut-uvotunicorr,v $
# Revision 1.2  2012/01/31 15:57:00  rwiegand
# Identify extension of input files.
#
# Revision 1.1  2008/04/17 21:27:43  rwiegand
# Prepared unit test.
#

use strict;

package Test::Unicorr;
use base qw(Task::HEAdas);
use Task qw(:codes);

use FileHandle;


# main
{
	my $tool = __PACKAGE__->new;

	$tool->unitTest;

	exit($tool->{code});
}


sub execute
{
	my ($self) = @_;

	$self->pilOptions(
			options => [
				qw(input=string output=string chatter=int)
			],
			get => 1,
			);

	$self->validate
		if $self->isValid;

	$self->runUvotunicorr
		if $self->isValid;
}


sub validate
{
	my ($self) = @_;

	# validate environment
	$self->validateEnvironment;

	# validate input/output directories
	foreach my $key (qw(input output)) {
		my $arg = $self->args->{$key};
		if (not -d $arg) {
			$self->fatal(BAD_INPUT,
				"invalid $key directory: $arg");
		}
	}
}


sub runUvotunicorr
{
	my ($self) = @_;

	my $args = $self->args;

	my $input = $args->{input};
	my $output = $args->{output};

	# note: make a working copy of the observation image
	# since it will be modified
	$self->shell("cp -f $input/skyobs.img $output/skyobs.img");
	$self->shell("chmod +w $output/skyobs.img");
	return if not $self->isValid;

	my @args = (
		obsfile   => "$output/skyobs.img",
		reffile   => "$input/skyref.img",

		obsreg    => "$input/obs.reg",
		refreg    => "$input/ref.reg",

		# cleanup   => 'no',
		chatter	  => 5,
	);

	# validate required files
	my %args = @args;
	$args{template} = "$input/template.fits";
	foreach my $key (qw(obsfile reffile )) {
		if (not -f $args{$key}) {
			$self->fatal(BAD_INPUT,
				"invalid $key file: $args{$key}");
		}
	}

	# identify input extension
	$args[1] .= '+0';
	$args[3] .= '+0';

	{
		my $command = $self->buildCommand('uvotunicorr', @args);
		$self->shell($command);
	}

	if ($self->isValid) {
		my $command = $self->buildCommand('ftverify',
			infile => $args{obsfile},
			outfile => 'STDOUT',
			);
		$self->shell($command);
	}

	if ($self->isValid) {
		my $command = $self->buildCommand('ftdiff',
			infile1 => $args{template},
			infile2 => $args{obsfile},
			reltol => 1e-6,
			exclude => 'CREATOR,DATE,DATASUM',
			);
		$self->shell($command);
	}
}

